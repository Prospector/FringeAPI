buildscript {
	repositories {
		jcenter()
		maven {
			name = 'Fabric'
			url = 'http://maven.modmuss50.me/'
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.0-SNAPSHOT"
	}
}

plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
}

apply plugin: net.fabricmc.loom.LoomGradlePlugin
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "FringeAPI"
version = "0.1.0"

minecraft {
}

dependencies {
	minecraft "com.mojang:minecraft:18w50a"
	mappings "net.fabricmc:yarn:18w50a.90"
	modCompile "net.fabricmc:fabric-loader:0.3.2.91"
	modCompile "net.fabricmc:fabric:0.1.3.68"
}

uploadArchives {
	repositories {
		mavenDeployer {
			//This is used when the build is done on a slave node
			if (project.hasProperty('mavenPass')) {
				println 'Uploading to remote Maven Repo'
				repository(url: "http://mavenupload.modmuss50.me/") {
					authentication(userName: "buildslave", password: project.getProperty('mavenPass'))
				}
			} else {
				//this is when the build is executed on the master node
				println 'Uploading to local Maven Repo'
				repository(url: "file:///var/www/maven/")
			}
			pom {
				groupId = project.group
				version = project.version
				artifactId = project.archivesBaseName
				project {
					name project.archivesBaseName
					packaging 'jar'
					description project.archivesBaseName
					url 'https://github.com/Prospector/FringeAPI'
					scm {
						url 'https://github.com/Prospector/FringeAPI'
						connection 'scm:git:git@github.com:Prospector/FringeAPI.git'
						developerConnection 'scm:git:git@github.com:Prospector/FringeAPI.git'
					}
				}
			}
		}
	}
}